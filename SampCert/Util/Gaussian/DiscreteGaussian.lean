/-
Copyright (c) 2024 Amazon.com, Inc. or its affiliates. All Rights Reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jean-Baptiste Tristan
-/
import Mathlib.NumberTheory.ModularForms.JacobiTheta.TwoVariable
import Mathlib.Probability.ProbabilityMassFunction.Basic

/-!
# Discrete Guassian Definitions

This file contains the mathematical definitions for the discrete Gaussian distribution.

It proves that the sum of discrete Gaussian values exists, and develops basic results
about the Fourier transform of the gaussian function.

-- TODO: Names in this file are not compliant with mathlib standards.
-/

noncomputable section

open Classical Complex Real Nat Filter Asymptotics FourierTransform
open Continuous

attribute [local instance] Real.fact_zero_lt_one

/--
â„-valued closed form for the Gaussian PMF over â„
-/
def gauss_term_â„ (Ïƒ Î¼ : â„) (x : â„) : â„ :=
  Real.exp ((-(x - Î¼)^2) / (2 * Ïƒ^2))

/--
â„‚-valued closed form for the Gaussian over â„ (as a continuous function).
-/
instance gauss_term_â„‚ (Ïƒ Î¼ : â„) : C(â„,â„‚) where
  toFun := fun x : â„ => ((gauss_term_â„ Ïƒ Î¼ x) : â„‚)
  continuous_toFun := by
    unfold gauss_term_â„
    simp
    apply Continuous.cexp
    apply Continuous.div_const
    apply Continuous.neg
    apply Continuous.pow
    apply Continuous.sub
    Â· apply continuous_ofReal
    Â· apply continuous_const

/--
Agreement between the â„‚-valued and â„-valued gaussian formulas over â„
-/
theorem gauss_term_swap (Ïƒ Î¼ : â„) (n : â„) :
  (gauss_term_â„‚ Ïƒ Î¼ n) = gauss_term_â„ Ïƒ Î¼ n := by
  simp [gauss_term_â„‚, gauss_term_â„]

-- def fourier_gauss_term (Ïƒ : â„) (x : â„) : â„‚ :=
--  Real.exp (- 2 * (Ï€ * Ïƒ * x)^2) / ((((Ï€ * (Ïƒ:â„)^2 * (2 : â„))â»Â¹) ^ (2 : â„)â»Â¹) : â„)


/--
Fourier transform of ``gauss_term``, namely ``sqrt(2Ï€Ïƒ^2) * exp(-2(Ï€ Ïƒ x)^2)``
-/
def fourier_gauss_term (Ïƒ : â„) (x : â„) : â„‚ :=
  Complex.exp (- 2 * (Ï€ * Ïƒ * x)^2) / (((Ï€ * (Ïƒ:â„‚)^2 * (2 : â„‚))â»Â¹) ^ (2 : â„‚)â»Â¹)

/--
Discrete gaussian formula
-/
def discrete_gaussian (Ïƒ Î¼ : â„) (x : â„) : â„ :=
  gauss_term_â„ Ïƒ Î¼ x / âˆ‘' x : â„¤, gauss_term_â„ Ïƒ Î¼ x

/--
A continuous map from the unit citcle â„\â„¤ to â„‚.

Obtained by summing the translates of ``gauss_term_â„‚``, i.e., ``fun x => âˆ‘ (z : â„¤), gauss_term_â„‚ Ïƒ 0 (x + z)``.
In a sense, this circle contains all possible (integer) sums of the shifted gaussian, since shiting a gaussian
by an integer will not change the sum of that gaussian at integer values.
-/
def gauss_series_circle (Ïƒ : â„) : C(UnitAddCircle, â„‚) :=
    âŸ¨((gauss_term_â„‚ Ïƒ 0).periodic_tsum_comp_add_zsmul 1).lift, continuous_coinduced_dom.mpr (map_continuous _)âŸ©

/--
Equality between the ``gauss_term`` and Jacobi Theta summands on N.
-/
theorem gauss_term_jacobi (Ïƒ Î¼ : â„) :
  (fun n : â„¤ => gauss_term_â„‚ Ïƒ Î¼ n) = fun n : â„¤ => (jacobiThetaâ‚‚_term n (Î¼ * ((2 : â„‚) * Ïƒ^2)â»Â¹ * Ï€â»Â¹ * Complex.Iâ»Â¹) (Complex.I * ((2 : â„‚) * Ïƒ^2)â»Â¹ * Ï€â»Â¹)) * Complex.exp (-Î¼ ^ 2 / (2 * Ïƒ^2)) := by
  ext n
  simp [gauss_term_â„‚, gauss_term_â„, jacobiThetaâ‚‚_term]
  rw [â† Complex.exp_add]
  congr 1
  ring_nf
  simp [pi_ne_zero]
  ring_nf

lemma jacobi_tau_positive {Ïƒ : â„} (h : Ïƒ â‰  0) :
  0 < (Complex.I * ((2 : â„‚) * Ïƒ^2)â»Â¹ * Ï€â»Â¹).im := by
  simp [pi_pos, h, sq]

/--
``gauss_term_â„‚`` is summable over â„¤.

Proven by specializing the summability from the Jacobi Theta function.
-/
theorem summable_gauss_term {Ïƒ : â„} (h : Ïƒ â‰  0) (Î¼ : â„) :
  Summable fun n : â„¤ => gauss_term_â„‚ Ïƒ Î¼ n := by
  rw [gauss_term_jacobi]
  apply Summable.mul_right
  rw [summable_jacobiThetaâ‚‚_term_iff]
  apply jacobi_tau_positive h

/--
``gauss_term_â„`` is summable over â„.
-/
theorem summable_gauss_term' {Ïƒ : â„} (h : Ïƒ â‰  0) (Î¼ : â„) :
  Summable fun n : â„¤ => gauss_term_â„ Ïƒ Î¼ n := by
  rw [â† Complex.summable_ofReal]
  conv =>
    right
    intro n
    rw [â† gauss_term_swap]
  apply summable_gauss_term h

/--
Big O asymptotic for ``gauss_term``.
-/
theorem asymptotics_gauss_term {Ïƒ : â„} (h : Ïƒ â‰  0) :
  gauss_term_â„‚ Ïƒ 0 =O[cocompact â„] (fun x => |x| ^ (-2 : â„)) := by
  apply IsLittleO.isBigO
  unfold gauss_term_â„‚ gauss_term_â„
  simp only [ContinuousMap.coe_mk, ofReal_zero, sub_zero]
  have Y : âˆ€ x : â„, -1 / (2 * Ïƒ ^ 2) * x ^ 2 = -x ^ 2 / (2 * Ïƒ ^ 2) := by
    intro x
    ring_nf
  conv =>
    left
    intro x
    rw [Complex.ofReal_exp]
    rw [â† Y]
  have P : (-(1 : â„‚) / (2 * Ïƒ^2)).re < 0 := by
    simp [div_eq_mul_inv, sq, h]
  have X := @cexp_neg_quadratic_isLittleO_abs_rpow_cocompact (-1 / (2 * Ïƒ^2)) P 0 (-2)
  simp only [zero_mul, add_zero] at X
  simp only [ofReal_mul, ofReal_div, ofReal_neg, ofReal_one, ofReal_ofNat,
    ofReal_pow]
  trivial

/--
Shifting the ``gauss_term`` function is eqivalent to shifting the mean.
-/
theorem gauss_term_shift (Ïƒ Î¼ : â„) (n Ï„ : â„¤) :
  gauss_term_â„‚ Ïƒ Î¼ (n + Ï„) = gauss_term_â„‚ Ïƒ (Î¼ - Ï„) n := by
  simp [gauss_term_â„‚, gauss_term_â„]
  congr 4
  ring_nf

/--
``fourier_gauss_term`` is the Fourier transform of the ``gauss_term`` with mean 0.
-/
theorem fourier_gauss_term_correspondance {Ïƒ : â„} (h : Ïƒ â‰  0) :
  (ğ“• (gauss_term_â„‚ Ïƒ 0)) = fourier_gauss_term Ïƒ := by
  have P : 0 < (Ï€ * (2 : â„‚) * Ïƒ^2)â»Â¹.re  := by
    simp [sq, h, pi_pos]
  have X := @fourierIntegral_gaussian_pi' (Ï€ * 2 * Ïƒ^2)â»Â¹ P 0
  have A : gauss_term_â„‚ Ïƒ 0 = fun x : â„ => cexp (-Ï€ * (Ï€ * (2 : â„‚) * Ïƒ ^ 2)â»Â¹ * x ^ 2 + 2 * Ï€ * 0 * x) := by
    ext x
    simp [gauss_term_â„‚, gauss_term_â„]
    congr 1
    ring_nf
    simp [pi_ne_zero]
  rw [A]
  rw [X]
  unfold fourier_gauss_term
  ext x
  ring_nf
  simp
  ring_nf
  simp

/--
Big O asymptotic for Fourier transform of ``gauss_term``.
-/
theorem asymptotics_fourier_gauss_term {Ïƒ : â„} (h : Ïƒ â‰  0) :
  (ğ“• (gauss_term_â„‚ Ïƒ 0)) =O[cocompact â„] (fun x => |x| ^ (-2 : â„)) := by
  rw [fourier_gauss_term_correspondance h]
  apply IsLittleO.isBigO
  unfold fourier_gauss_term
  conv =>
    left
    intro x
    rw [division_def]
    rw [mul_comm]
  apply IsLittleO.const_mul_left
  have P : (-(2 : â„‚) * Ï€ ^ 2 * Ïƒ^2).re < 0 := by
    simp [h, pow_two, pi_ne_zero]

  have X := @cexp_neg_quadratic_isLittleO_abs_rpow_cocompact (-2 * Ï€ ^ 2 * Ïƒ^2) P 0 (-2)
  simp only [zero_mul, add_zero] at X
  have Y : âˆ€ x : â„, -2 * Ï€ ^ 2 * Ïƒ ^ 2 * x ^ 2 = (-(2 : â„‚) * (Ï€ * Ïƒ * x) ^ 2) := by
    intro x
    simp
    ring_nf
  conv =>
    left
    intro x
    rw [â† Y]
  trivial

/--
Poisson sum formula specialized to ``gauss_term``.
-/
theorem poisson_gauss_term {Ïƒ : â„} (h : Ïƒ â‰  0) (x : â„) :
  (âˆ‘' n : â„¤, gauss_term_â„‚ Ïƒ 0 (x + n)) = âˆ‘' (n : â„¤), (ğ“• (gauss_term_â„‚ Ïƒ 0)) n * (@fourier 1 n) x := by
  have B := asymptotics_gauss_term h
  have C := asymptotics_fourier_gauss_term h
  have D : (1 : â„) < 2 := one_lt_two
  have X := @Real.tsum_eq_tsum_fourierIntegral_of_rpow_decay (gauss_term_â„‚ Ïƒ 0) ((gauss_term_â„‚ Ïƒ 0).continuous_toFun) 2 D B C x
  rw [â† X]

/--
Fourier transform for ``gauss_term`` is summable.
-/
theorem summable_fourier_gauss_term {Ïƒ : â„} (h : Ïƒ â‰  0) :
  Summable fun n : â„¤ => ğ“• (gauss_term_â„‚ Ïƒ 0) n := by
  have A := Real.summable_abs_int_rpow one_lt_two
  have B := @IsBigO.comp_tendsto â„ â„¤ â„‚ â„ _ _ (ğ“• (gauss_term_â„‚ Ïƒ 0)) (fun (x : â„) => ((|x| ^ (-2 : â„)) : â„)) (cocompact â„) (asymptotics_fourier_gauss_term h) Int.cast cofinite Int.tendsto_coe_cofinite
  have C := @summable_of_isBigO â„¤ â„‚ _ _ (fun z : â„¤ => ğ“• (gauss_term_â„‚ Ïƒ 0) z) (fun x : â„¤ => |(x : â„)| ^ (-2 : â„)) A B
  exact C

/--
Fourier transform of ``gauss_term_â„‚`` at the integers is the same as the Fourier coeficients of the sum of the translates.
-/
theorem fourier_coeff_correspondance {Ïƒ : â„} (h : Ïƒ â‰  0) (n : â„¤) :
  fourierCoeff (gauss_series_circle Ïƒ) n = ğ“• (gauss_term_â„‚ Ïƒ 0) n := by
  apply Real.fourierCoeff_tsum_comp_add
  apply (fun K => summable_of_isBigO (Real.summable_abs_int_rpow one_lt_two)
  ((isBigO_norm_restrict_cocompact âŸ¨_ , ((gauss_term_â„‚ Ïƒ 0).continuous_toFun)âŸ©  (zero_lt_one.trans one_lt_two) (asymptotics_gauss_term h) K).comp_tendsto
  Int.tendsto_coe_cofinite))

/--
Fourier series obtained by evaluating the Fourier transform of ``gauss_term`` exists (i.e., is summable).
-/
theorem summable_fourier_gauss_term' {Ïƒ : â„} (h : Ïƒ â‰  0) (Î¼ : â„) :
  Summable fun (n : â„¤) => ğ“• (gauss_term_â„‚ Ïƒ 0) n * (@fourier 1 n) (-Î¼) := by
  have A : Summable fun n : â„¤ => fourierCoeff (gauss_series_circle Ïƒ) n := by
    conv =>
      right
      intro n
      rw [fourier_coeff_correspondance h]
    apply summable_fourier_gauss_term h
  have B := has_pointwise_sum_fourier_series_of_summable A (- Î¼)
  existsi ((gauss_series_circle Ïƒ) (-Î¼))
  conv =>
    left
    intro n
    rw [â† fourier_coeff_correspondance h]
  exact B

lemma gauss_term_pos (Ïƒ Î¼ : â„) (n : â„¤) :
  0 < (gauss_term_â„ Ïƒ Î¼) n := by
  unfold gauss_term_â„
  apply exp_pos

lemma gauss_term_noneg (Ïƒ Î¼ : â„) (n : â„¤) :
  0 â‰¤ (gauss_term_â„ Ïƒ Î¼) n := by
  unfold gauss_term_â„
  apply le_of_lt (gauss_term_pos Ïƒ Î¼ n)

/--
The sum of ``gauss_term_â„`` is positive.
-/
theorem sum_gauss_term_pos {Ïƒ : â„} (h : Ïƒ â‰  0) (Î¼ : â„) :
  0 < (âˆ‘' (x : â„¤), (gauss_term_â„ Ïƒ Î¼) x) := by
  apply tsum_pos (summable_gauss_term' h Î¼) _ 0
  Â· apply gauss_term_pos
  Â· intro i
    apply gauss_term_noneg

/--
The sum of ``gauss_term_â„`` is nonnegative.
-/
theorem sum_gauss_term_nonneg {Ïƒ : â„} (h : Ïƒ â‰  0) (Î¼ : â„) :
  0 â‰¤ (âˆ‘' (x : â„¤), (gauss_term_â„ Ïƒ Î¼) x) := by
  apply le_of_lt (sum_gauss_term_pos h Î¼)

/--
The sum of ``gauss_term_â„`` is nonzero.
-/
theorem sum_gauss_term_ne_zero {Ïƒ : â„} (h : Ïƒ â‰  0) (Î¼ : â„) :
  âˆ‘' (n : â„¤), gauss_term_â„ Ïƒ Î¼ n â‰  0 := by
  apply Ne.symm (_root_.ne_of_lt (sum_gauss_term_pos h Î¼))

/--
The discrete gaussian is positive.
-/
theorem discrete_gaussian_pos {Ïƒ : â„} (h : Ïƒ â‰  0) (Î¼ : â„) (n : â„¤) :
  0 < discrete_gaussian Ïƒ Î¼ n := by
  unfold discrete_gaussian
  rw [div_pos_iff]
  left
  constructor
  Â· apply gauss_term_pos
  Â· apply sum_gauss_term_pos h Î¼

/--
The discrete gaussian is nonnegative.
-/
theorem discrete_gaussian_nonneg {Ïƒ : â„} (h : Ïƒ â‰  0) (Î¼ : â„) (n : â„¤) :
  0 â‰¤ discrete_gaussian Ïƒ Î¼ n := by
  apply le_of_lt (discrete_gaussian_pos h Î¼ n)

/--
The discrete Gaussian with mean zero is summable.
-/
theorem discrete_gaussian_summable {Ïƒ : â„} (h : Ïƒ â‰  0) :
  Summable fun (n : â„¤) => discrete_gaussian Ïƒ 0 n := by
  unfold discrete_gaussian
  apply Summable.div_const
  apply summable_gauss_term' h

/--
The discrete Gaussian is summable.
-/
theorem discrete_gaussian_summable' {Ïƒ : â„} (h : Ïƒ â‰  0) (Î¼ : â„) :
  Summable fun (n : â„¤) => discrete_gaussian Ïƒ Î¼ n := by
  unfold discrete_gaussian
  apply Summable.div_const
  apply summable_gauss_term' h

/--
The discrete Gaussian is a proper distribution.
-/
theorem discrete_gaussian_normalizes {Ïƒ : â„} (h : Ïƒ â‰  0) (Î¼ : â„) :
  (âˆ‘' n : â„¤, discrete_gaussian Ïƒ Î¼ n) = 1 := by
  unfold discrete_gaussian
  rw [tsum_div_const]
  rw [div_eq_one_iff_eq]
  apply sum_gauss_term_ne_zero h


/--
Discrete Gaussian cast to ENNReal has sum 1
-/
def discrete_gaussian_normal {Ïƒ : â„} (h : Ïƒ â‰  0) (Î¼ : â„) : HasSum (fun z : â„¤ => ENNReal.ofReal (discrete_gaussian Ïƒ Î¼ z)) 1 := by
  rw [Summable.hasSum_iff ENNReal.summable]
  rw [<- ENNReal.ofReal_tsum_of_nonneg]
  case hf_nonneg =>
    exact fun n => discrete_gaussian_nonneg h Î¼ n
  case hf =>
    apply discrete_gaussian_summable'
    apply h
  simp [discrete_gaussian_normalizes h Î¼]

/--
The discrete Gaussian as a PMF
-/
def discrete_gaussian_pmf {Ïƒ : â„} (h : Ïƒ â‰  0) (Î¼ : â„) : PMF â„¤ :=
  âŸ¨ (fun z : â„¤ => ENNReal.ofReal (discrete_gaussian Ïƒ Î¼ z)) , discrete_gaussian_normal h Î¼ âŸ©
